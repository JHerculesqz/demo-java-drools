package rules

import com.huawei.client.provider.subprovider.vo.TestVo;

rule birthdayPoint
	// 过生日，则加10分，并且将当月交易比数翻倍后再计算积分
	salience 100
	lock-on-active true
	when
		$testVo : TestVo(birthDay == true)
	then
		$testVo.setPoint($testVo.getPoint()+10);
		$testVo.setBuyNums($testVo.getBuyNums()*2);
		$testVo.setBuyMoney($testVo.getBuyMoney()*2);
		$testVo.setBillThisMonth($testVo.getBillThisMonth()*2);
		
		$testVo.recordPointLog($testVo.getUserName(),"birthday");
end

rule billThisMonthPoint
	// 2011-01-08 - 2011-08-08每月信用卡还款3次以上，每满3笔赠送30分
	salience 99
	lock-on-active true
	date-effective "2011-01-08 23:59:59"
	date-expires "2011-08-08 23:59:59"
	when
		$testVo : TestVo(billThisMonth >= 3)
	then
		$testVo.setPoint($testVo.getPoint()+$testVo.getBillThisMonth()/3*30);
		$testVo.recordPointLog($testVo.getUserName(),"billThisMonth");
end

rule buyMoneyPoint
	// 当月购物总金额100以上，每100元赠送10分
	salience 98
	lock-on-active true
	when
		$testVo : TestVo(buyMoney >= 100)
	then
		$testVo.setPoint($testVo.getPoint()+ (int)$testVo.getBuyMoney()/100 * 10);
		$testVo.recordPointLog($testVo.getUserName(),"buyMoney");
end

rule buyNumsPoint
	// 当月购物次数5次以上，每五次赠送50分
	salience 97
	lock-on-active true
	when
		$testVo : TestVo(buyNums >= 5)
	then
		$testVo.setPoint($testVo.getPoint()+$testVo.getBuyNums()/5 * 50);
		$testVo.recordPointLog($testVo.getUserName(),"buyNums");
end

rule allFitPoint
	// 特别的，如果全部满足了要求，则额外奖励100分
	salience 96
	lock-on-active true
	when
		$testVo:TestVo(buyNums >= 5 && billThisMonth >= 3 && buyMoney >= 100)
	then
		$testVo.setPoint($testVo.getPoint()+ 100);
		$testVo.recordPointLog($testVo.getUserName(),"allFit");
end